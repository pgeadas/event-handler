plugins {
    id 'groovy'
    id 'application'
}

defaultTasks 'jar'

group = 'io.seqera'
version = '0.0.1'
mainClassName = 'io.seqera.events.App'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.2'
    implementation 'org.apache.groovy:groovy-sql:4.0.2'
    implementation 'org.apache.groovy:groovy-yaml:4.0.2'
    implementation 'org.apache.groovy:groovy-json:4.0.2'
    implementation group: 'org.hsqldb', name: 'hsqldb', version: '2.7.2'
    // using this lib only because of the chained comparators in the InMemoryEventRepository, needed for multiple orderBy
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.3.1'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.4-M1-groovy-4.0'
}

application {
    mainClass = mainClassName
}


tasks.register('customFatJar', Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnitPlatform()
}

